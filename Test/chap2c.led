{
	revised logic programming version of sort procedure
	with starting clauses written as control flow statements
	from chapter 2
	written by tim budd
}

include "std.led";
include "intList.led";

function insertion(startList : intList,
	x : integer, byRef newList : intList)->relation;
var
	temp : intList;
begin
	if empty(startList) then
		return newList <- intList(x, NIL)
	else
		if x < head(startList) then
			return newList <- intList(x, startList)
		else
			return insertion(tail(startList), x, temp) &
				newList <- intList(head(startList), temp);
end;

function sort(startList:intList, byRef endList:intList)->relation;
var
	temp : intList;
begin
	if empty(startList) then
		return endList <- NIL
	else
		return sort(tail(startList), temp) & 
			insertion(temp, head(startList), endList);
end;

var
	in, out : intList;
begin

	in := intList(2, intList(5, intList(1, intList(4, intList(7, NIL)))));

	if sort(in, out) then begin
		print("sorted output\n");
		listPrint(out);
	end
	else
		print("unable to sort!");
end;

	
